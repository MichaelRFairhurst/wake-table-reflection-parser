import WakeType;
import WakeProperty;
import Map;
import Annotation;

every WakeClass is:

	needs public Map{Text, WakeProperty} properties;

	with public Bool isAbstract = false;
	with public Text classname = "";
	with public WakeType[] needTypes = [];
	with public WakeType[] parameters = [];
	with public WakeType[] appliedParameters = [];
	with public Annotation[] = [];

	with public Text? parentclass = nothing;
	with public Text[] interfaces = [];

	Text[] -- getApiImports() {
		var Text[] = [];
		foreach(properties.getKeys()) { // @Todo foreach as
			var WakeProperty? = properties.get(Text);

			if WakeProperty exists {
				foreach($Text in WakeProperty.getNeededImports()) {

					if(!Text[].contains($Text)) Text[].push($Text);
				}
			}
		}

		return Text[];
	}
