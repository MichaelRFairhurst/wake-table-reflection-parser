module treflect;

import io.File;
import std.Map;
import treflect.WakeType;
import treflect.WakeClass;
import treflect.WakeProperty;
import treflect.ReflectionProvider;
import treflect.ImportFilterer;
import treflect.WakeClassType;
import treflect.WakeListType;
import treflect.WakeLambdaType;
import treflect.WakeParameterizedType;
import treflect.WakeOptionalType;
import treflect.Annotation;
import treflect.AnnotationVal;
import treflect.UnusableTableFileVersionException;

every TableFileReader is:

	needs ReflectionProvider;

	with public WakeClass[] addingClass = [];

	with Num newestVersion = 5;
	with Num oldestVersion = 5;

	WakeClass -- readFrom(File) {
		var Num version = File.readUnsignedChar();

		if(version < oldestVersion || version > newestVersion) {
			throw UnusableTableFileVersionException(version, newestVersion) from ReflectionProvider;
		}

		var WakeClass from ReflectionProvider;
		WakeClass.modulename = readTextWithLengthTag(File);
		WakeClass.classname = readTextWithLengthTag(File);

		if(File.readUnsignedChar() != 0) WakeClass.isAbstract = true;

		var tag Num = 0;
		while((tag := File.readUnsignedChar()) != 0) {
			var Text = File.read(tag); // get method name
			var Text casing = readTextWithLengthTag(File); // get Casing
			var flags Num = File.readUnsignedChar();
			var WakeType = readType(File);
			var WakeProperty from ReflectionProvider;

			WakeProperty.WakeType = WakeType;
			WakeProperty.name = Text;
			WakeProperty.flags = flags;
			WakeProperty.casing = casing.split('#');
			WakeProperty.Annotation[] = getAnnotations(File);
			WakeClass.properties.put(Text, WakeProperty);
		}

		while((tag := File.readUnsignedChar()) != 0) {
			var Text = File.read(tag); // get classname
			var Num = File.readUnsignedChar();
			if(Num != 0) WakeClass.parentclass = Text;
			else WakeClass.interfaces.push(Text);
		}

		// For this we need file.eof()
		while((tag := File.readUnsignedChar()) != 0) {
			WakeClass.parameters.push(readType(File));
		}

		WakeClass.Annotation[] = getAnnotations(File);

		return WakeClass;

	}

	WakeType -- readType(File) {
		return readTypeByTag(File.readUnsignedChar(), File);
	}

	WakeType -- readTypeByTag(Num tag, File) {
		var WakeType from ReflectionProvider;
		WakeType.type = tag;
		if(tag == 1) {
			var WakeLambdaType from ReflectionProvider;
			if((File.readUnsignedChar()) != 0) {
				WakeLambdaType.returntype = readType(File);
			}
			while((tag := File.readUnsignedChar()) != 0) {
				WakeLambdaType.arguments.push(readTypeByTag(tag, File));
			}
			WakeType.WakeLambdaType = WakeLambdaType;
		} else if(tag == 2) {
			var WakeClassType from ReflectionProvider;
			WakeClassType.classname = readTextWithLengthTag(File);
			while((tag := File.readUnsignedChar()) != 0) {
				WakeClassType.parameters.push(readTypeByTag(tag, File));
			}
			File.readUnsignedChar(); // shadow
			WakeType.WakeClassType = WakeClassType;
		} else if(tag == 3) {
			var WakeParameterizedType from ReflectionProvider;
			WakeParameterizedType.label = readTextWithLengthTag(File); // label
			if((tag := File.readUnsignedChar()) != 0) {
				WakeParameterizedType.upperbound = readType(File);
			}
			if((tag := File.readUnsignedChar()) != 0) {
				WakeParameterizedType.lowerbound = readType(File);
			}
			File.readUnsignedChar(); // shadow
			WakeType.WakeParameterizedType = WakeParameterizedType;
		} else if(tag == 7) {
			ReflectionProvider.contained = readType(File);
			var Num levels = File.readUnsignedChar();
			var WakeListType from ReflectionProvider; // @Todo ctor args
			WakeListType.levels = levels;
			WakeType.WakeListType = WakeListType;
		} else if(tag == 8) {
			ReflectionProvider.contained = readType(File);
			var Num levels = File.readUnsignedChar();
			var WakeOptionalType from ReflectionProvider; // @Todo ctor args
			WakeOptionalType.levels = levels;
			WakeType.WakeOptionalType = WakeOptionalType;
		}
		WakeType.alias = readTextWithLengthTag(File); // read alias
		WakeType.specialty = readTextWithLengthTag(File);

		return WakeType;
	}

	Text -- readTextWithLengthTag(File) {
		var Num = File.readUnsignedChar();
		if(Num == 0) return "";
		return File.read(Num);
	}

	Annotation[] -- getAnnotations(File) {
		var Annotation[] = [];

		while(true) {
			var Num = File.readUnsignedChar();
			if(Num == 0) break;

			var Text name = File.read(Num);
			var Annotation(name) from ReflectionProvider;
			Annotation[].push(Annotation);

			while(true) {
				Num = File.readUnsignedChar();
				if(Num == 0) break;

				var AnnotationVal from ReflectionProvider;
				Annotation.AnnotationVal[].push(AnnotationVal);
				if(Num == 1) {
					AnnotationVal.Text = readTextWithLengthTag(File);
				} else if(Num == 2) {
					// aaaah!!
				} else if(Num == 3) {
					AnnotationVal.Bool = true if File.readUnsignedChar() else false;
				}
			}
		}

		return Annotation[];
	}
